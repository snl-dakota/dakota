dnl cf/wingcc.ac for PLplot  -*- autoconf -*-
dnl
dnl ------------------------------------------------------------------------
dnl Find libgdi32 library and header required for building wingcc driver.
dnl Written by Andrew Roach following what is done for gd driver.
dnl ------------------------------------------------------------------------
dnl
dnl Copyright (C) 1994, 2004  Maurice LeBrun
dnl Copyright (C) 2002, 2003, 2004  Rafael Laboissiere
dnl Copyright (C) 2002, 2003, 2004  Alan W. Irwin
dnl Copyright (C) 2003, 2004  Joao Cardoso
dnl Copyright (C) 2004  Andrew Roach
dnl
dnl This file is part of PLplot.
dnl
dnl PLplot is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU Library General Public License as published
dnl by the Free Software Foundation; version 2 of the License.
dnl
dnl PLplot is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General Public License
dnl along with the file PLplot; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA

PL_ARG_WITH(gdi32-incdir, [User-specified path to the libgdi32 header files], [])
if test "$enable_wingcc" = "yes"; then
    if test -z "$with_gdi32_incdir"; then
        AC_MSG_CHECKING(for libgdi32 header files)
        AC_PREPROC_IFELSE([#include <windows.h>],
	[ with_gdi32_incdir=default
	  AC_MSG_RESULT([found in $with_gdi32_incdir]) ], [
        incdirs="\
        /usr/include \
        /usr/local/include"

        for dir in $incdirs; do
            if test -r "$dir/windows.h"; then
               with_gdi32_incdir="$dir"
               break
            fi
        done
        if test -z "$with_gdi32_incdir"; then
            AC_MSG_RESULT(not found)
            AC_MSG_WARN([libgdi32 header files not found, setting enable_wingcc=no])
            with_gdi32_incdir=default
            enable_wingcc=no
	else
	    AC_MSG_RESULT([found in $with_gdi32_incdir])
        fi ])
    else
    	AC_MSG_NOTICE("User-specified path to the libgdi32 header files is $with_gdi32_incdir")
    fi
    if test "$with_gdi32_incdir" = "/usr/include"; then
        with_gdi32_incdir=default
    fi
    GDI32INCCMD=""
    GDI32INCPATH=""
    PL_ADD_TO_INCS($with_gdi32_incdir, GDI32INCCMD, GDI32INCPATH)
fi

PL_ARG_WITH(gdi32-libdir, [User-specified path to libgdi32], [])
if test "$enable_wingcc" = "yes"; then
    if test -z "$with_gdi32_libdir"; then
        AC_CHECK_LIB(gdi32, main, [ with_gdi32_libdir=default ], [
        libdirs="\
        /usr/lib \
        /usr/local/lib"

        for dir in $libdirs; do
            if test -r "$dir/libgdi32.so" -o -r "$dir/libgdi32.a"; then
                with_gdi32_libdir="$dir"
                break
            fi
        done
        if test -z "$with_gdi32_libdir"; then
            AC_MSG_WARN([gdi32 library not found, setting enable_wingcc=no])
            with_gdi32_libdir=default
            enable_wingcc=no
        fi ])
    else
    	AC_MSG_NOTICE("User-specified path to libgdi32 is $with_gdi32_libdir")
    fi
    if test "$with_gdi32_libdir" = "/usr/lib"; then
        with_gdi32_libdir=default
    fi
    GDI32LIBSTR=-lgdi32
    GDI32LIBCMD=""
    GDI32LIBPATH=""
    PL_ADD_TO_LIBS($with_gdi32_libdir, $GDI32LIBSTR, GDI32LIBCMD, GDI32LIBPATH)
fi

AC_SUBST(GDI32INCCMD)
AC_SUBST(GDI32LIBCMD)
