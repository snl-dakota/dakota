dnl cf/instdirs.ac for PLplot  -*- autoconf -*-
dnl
dnl Set default installation directoires.  BUILD_DIR is also defined here.
dnl
dnl Copyright (C) 1994, 2004  Maurice LeBrun
dnl Copyright (C) 2002, 2003, 2004  Rafael Laboissiere
dnl Copyright (C) 2002, 2003, 2004  Alan W. Irwin
dnl Copyright (C) 2003, 2004  Joao Cardoso
dnl
dnl This file is part of PLplot.
dnl
dnl PLplot is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU Library General Public License as published
dnl by the Free Software Foundation; version 2 of the License.
dnl
dnl PLplot is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General Public License
dnl along with the file PLplot; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA

test -z "$DATA_DIR" && DATA_DIR='${datadir}/${PACKAGE}${VERSION}'
AC_SUBST(DATA_DIR)

test -z "$LIB_DIR" && LIB_DIR='${libdir}'
AC_SUBST(LIB_DIR)

test -z "$INCLUDE_DIR" && INCLUDE_DIR='${prefix}/include/plplot'
AC_SUBST(INCLUDE_DIR)

test -z "$BIN_DIR" && BIN_DIR='${bindir}'

test -z "$TCL_DIR" && TCL_DIR="$DATA_DIR/tcl"
AC_SUBST(TCL_DIR)

test -z "$DRV_DIR" && DRV_DIR="$LIB_DIR/${PACKAGE}${VERSION}/drivers${LIB_TAG}"
AC_SUBST(DRV_DIR)

### Documentation directory
test -z "$DOC_DIR" && DOC_DIR='${datadir}/doc/plplot'
AC_SUBST(DOC_DIR)

# On some systems pwd produces a logical path rather than 
# a physical path which is different to getcwd, which always
# returns a physical path. Try to work around this.

if test -x "/bin/pwd" ; then
  BUILD_DIR=`/bin/pwd`
elif pwd -P ; then
  BUILD_DIR=`pwd -P`
else
  BUILD_DIR=`pwd`
fi
AC_SUBST(BUILD_DIR)

# The following *_CMD variables are needed in examples/*/Makefile.examples.in

PL_EXPAND_EXPRESSION(LIB_DIR_CMD, -L$LIB_DIR)
AC_SUBST(LIB_DIR_CMD)
PL_EXPAND_EXPRESSION(INCLUDE_DIR_CMD, -I$INCLUDE_DIR)
AC_SUBST(INCLUDE_DIR_CMD)

# The following *_HARDDIR variables are needed in bindings/*/pkgIndex.tcl.in

PL_EXPAND_EXPRESSION(LIB_HARDDIR, $LIB_DIR)
AC_SUBST(LIB_HARDDIR)
PL_EXPAND_EXPRESSION(DRV_HARDDIR, $DRV_DIR)
AC_SUBST(DRV_HARDDIR)

# Location substitutions for header files as well

PL_EXPAND_EXPRESSION(TMPVAR, $DATA_DIR)
AC_DEFINE_UNQUOTED(DATA_DIR, "$TMPVAR", [Location of package data files])

PL_EXPAND_EXPRESSION(TMPVAR, $DRV_DIR)
AC_DEFINE_UNQUOTED(DRV_DIR, "$TMPVAR", [Location of dynamically loaded drivers])

PL_EXPAND_EXPRESSION(TMPVAR, $BIN_DIR)
AC_DEFINE_UNQUOTED(BIN_DIR, "$TMPVAR", [Location of executables])

PL_EXPAND_EXPRESSION(TMPVAR, $TCL_DIR)
AC_DEFINE_UNQUOTED(TCL_DIR, "$TMPVAR", [Location of Tcl stuff])

AC_DEFINE_UNQUOTED(BUILD_DIR, "$BUILD_DIR", [Location of Build tree])
