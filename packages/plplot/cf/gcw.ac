dnl cf/gcw.ac for PLplot  -*- autoconf -*-
dnl
dnl ------------------------------------------------------------------------
dnl Gnome/GTK configuration tests
dnl Added by Tom Duck
dnl ------------------------------------------------------------------------
dnl
dnl Copyright (C) 2005  Thomas J. Duck
dnl
dnl This file is part of PLplot.
dnl
dnl PLplot is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU Library General Public License as published
dnl by the Free Software Foundation; version 2 of the License.
dnl
dnl PLplot is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General Public License
dnl along with the file PLplot; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
dnl
dnl ------------------------------------------------------------------------

dnl Configuring the gcw driver and the plplotcanvas support needs the
dnl pkg-config program

dnl Test for pkg-config

AC_CHECK_PROG(PKG_CONFIG_CHECK, pkg-config, yes, no)

if test "$PKG_CONFIG_CHECK" = "no"; then

  AC_MSG_WARN([pkg-config not found])
  AC_MSG_WARN([Setting enable_gcw and enable_pygcw to no])
  
  enable_gcw=no
  enable_pygcw=no

else

dnl Note that Gnome2 uses pkg-config as a key part of the
dnl configure process; there is no way of getting around it


dnl Flag whether or not to include the python binding
if test "$enable_python" = "yes" -a "$enable_gcw" = "yes"; then
  enable_pygcw=yes
else
  enable_pygcw=no
fi


dnl Test for libraries, header files, etc etc etc
if test "$enable_gcw" = "yes"; then

  dnl Test for libgnomeui required by gcw/plplotcanvas
  PKG_CHECK_MODULES(GCW,libgnomeui-2.0,, enable_gcw=no )

  dnl Test for libgnomeprintui required by gcw/plplotcanvas
  PKG_CHECK_MODULES(GCW,libgnomeprintui-2.2,, enable_gcw=no )


  dnl Continue if the required libraries exist
  if test "$enable_gcw" = "no"; then

      AC_MSG_WARN([required libraries not found via pkg-config;]) 
      AC_MSG_WARN([  gcw/plplotcanvas driver disabled])

  else

    dnl Test for the header files required by gcw/plplotcanvas
    headers=yes
    CPPFLAGS="$CPPFLAGS $GCW_CFLAGS"

    AC_CHECK_HEADERS([math.h],, headers=no )
    AC_CHECK_HEADERS([glib.h],, headers=no )
    AC_CHECK_HEADERS([gtk/gtk.h],, headers=no )
    AC_CHECK_HEADERS([libgnomecanvas/libgnomecanvas.h],, headers=no )
    AC_CHECK_HEADERS([libart_lgpl/libart.h],, headers=no )
    AC_CHECK_HEADERS([libgnomeprint/gnome-print.h],, headers=no )

    if test "$headers" = "no"; then
      AC_MSG_WARN([required headers not found; gcw/plplotcanvas])
      AC_MSG_WARN([  driver disabled])
      enable_gcw=no
    fi

  fi


  dnl Test for python binding requirements
  if test "$enable_gcw" = "yes" -a "$enable_pygcw" = "yes"; then

    dnl Test for the required libraries

    dnl Test for pygtk required by gcw/plplotcanvas
    PKG_CHECK_MODULES(PYGCW,pygtk-2.0,, enable_pygcw=no )

    dnl Test for gnome-python required by gcw/plplotcanvas
    PKG_CHECK_MODULES(GNOME_PYTHON, gnome-python-2.0, [], enable_pygcw=no)

    if test "$enable_pygcw" = "no"; then

      AC_MSG_WARN([required libraries not found via pkg-config;])
      AC_MSG_WARN([  python binding for gcw/plplotcanvas will not])
      AC_MSG_WARN([  be generated])

    else

      dnl Test for the required headers
      CPPFLAGS="$CPPFLAGS $PYGCW_CFLAGS"
      AC_CHECK_HEADERS([glib-object.h],, headers=no )

      if test "$headers" = "no"; then
        AC_MSG_WARN([required headers not found; python binding for])
        AC_MSG_WARN([  gcw/plplotcanvas will not be generated])
        enable_pygcw=no
      else

        dnl Test for pygtk/codegen, defs, dsextras
        AC_MSG_CHECKING(for pygtk/codegen and friends)
        if test -z `pkg-config --variable=codegendir pygtk-2.0`; then
          AC_MSG_RESULT(not found)
          AC_MSG_WARN([pygtk/codegen not found; python binding for])
          AC_MSG_WARN([  gcw/plplotcanvas will not be generated])
          enable_pygcw=no
        else
          AC_MSG_RESULT(yes)
        fi
      fi
    fi
  else
    enable_pygcw=no
  fi
fi

#
# GCW_LIBS all libs needed by the gcw driver.


fi # if test "$with_pkg_config""


dnl The choice of the names GCW_FLAGS and GCW_LIBS is made by
dnl the PKG_CHECK_MODULES macro.

AC_SUBST(GCW_CFLAGS)
AC_SUBST(GCW_LIBS)
AC_SUBST(PYGCW_CFLAGS)
AC_SUBST(PYGCW_LIBS)

AM_CONDITIONAL(enable_gcw, [test "$enable_gcw" = yes])
AM_CONDITIONAL(enable_pygcw, [test "$enable_pygcw" = yes])
