dnl cf/aqt.ac for PLplot  -*- autoconf -*-
dnl
dnl -------------------------------------------------------------------------
dnl Find libaquaterm library and headers required to build aquaterm driver.
dnl Written by Per Persson following Alan W. Irwin
dnl -------------------------------------------------------------------------
dnl
dnl Copyright (C) 2004  Per Persson
dnl
dnl This file is part of PLplot.
dnl
dnl PLplot is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU Library General Public License as published
dnl by the Free Software Foundation; version 2 of the License.
dnl
dnl PLplot is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General Public License
dnl along with the file PLplot; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA

AQTLIBSTR=-laquaterm

if test "$enable_aqt" = "yes"; then
    AC_MSG_CHECKING(for AquaTerm header files)
    if test -z "$AQTINCDIR"; then
        AC_PREPROC_IFELSE([#include <aquaterm/AQTAdapter.h>],
	[ AQTINCDIR=default
	  AC_MSG_RESULT([found in $AQTINCDIR]) ], [
        incdirs="\
        /usr/include \
        /usr/local/include"

        for dir in $incdirs; do
            if test -r "$dir/aquaterm/AQTAdapter.h"; then
               AQTINCDIR="$dir"
               break
            fi
        done
        if test -z "$AQTINCDIR"; then
	    AC_MSG_RESULT(not found)
            AC_MSG_WARN([AquaTerm header files not found, setting enable_aqt=no])
            AQTINCDIR=default
            enable_aqt=no
	else
	    AC_MSG_RESULT([found in $AQTINCDIR])
        fi ])
    fi
fi

if test "$enable_aqt" = "yes"; then
    if test "$AQTINCDIR" = "/usr/include"; then
        AQTINCDIR=default
    fi
    AQTINCCMD="-ObjC"
    AQTINCPATH=""
    PL_ADD_TO_INCS($AQTINCDIR, AQTINCCMD, AQTINCPATH)

    if test -z "$AQTLIBDIR"; then
        AC_CHECK_LIB(aquaterm, aqtInit, [ AQTLIBDIR=default ], [
        libdirs="\
        /usr/lib \
        /usr/local/lib"

        for dir in $libdirs; do
            if test -r "$dir/libaquaterm.dylib"; then
                AQTLIBDIR="$dir"
                break
            fi
        done
        if test -z "$AQTLIBDIR"; then
            AC_MSG_WARN([libaquaterm not found, setting enable_aqt=no])
            AQTLIBDIR=default
            enable_aqt=no

        fi ], -lobjc)
    fi
fi

if test "$enable_aqt" = "yes"; then
    if test "$AQTLIBDIR" = "default"; then
        AQTLIBDIR=/usr/lib
    fi
    if test "$AQTLIBDIR" = "/usr/lib"; then
        AQTLIBDIR=default
    fi
    AQTLIBCMD="$AQTLIBSTR -lobjc -Wl,-framework -Wl,Foundation"
    AQTLIBPATH=""
    PL_ADD_TO_LIBS($AQTLIBDIR, $AQTLIBSTR, AQTLIBCMD, AQTLIBPATH)

fi


AC_SUBST(AQTINCCMD)
AC_SUBST(AQTLIBCMD)

