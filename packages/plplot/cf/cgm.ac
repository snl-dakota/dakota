dnl cf/cgm.ac for PLplot  -*- autoconf -*-
dnl
dnl ------------------------------------------------------------------------
dnl Find libcd library and header required for building cgm driver.
dnl Written by Alan W. Irwin following what is done for gd driver.
dnl ------------------------------------------------------------------------
dnl
dnl Copyright (C) 1994, 2004  Maurice LeBrun
dnl Copyright (C) 2002, 2003, 2004  Rafael Laboissiere
dnl Copyright (C) 2002, 2003, 2004  Alan W. Irwin
dnl Copyright (C) 2003, 2004  Joao Cardoso
dnl
dnl This file is part of PLplot.
dnl
dnl PLplot is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU Library General Public License as published
dnl by the Free Software Foundation; version 2 of the License.
dnl
dnl PLplot is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General Public License
dnl along with the file PLplot; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA

PL_ARG_WITH(cd-incdir, [User-specified path to the libcd header files], [])
if test "$enable_cgm" = "yes"; then
    if test -z "$with_cd_incdir"; then
        AC_MSG_CHECKING(for libcd header files)
        AC_PREPROC_IFELSE([#include <cd.h>],
	[ with_cd_incdir=default
	  AC_MSG_RESULT([found in $with_cd_incdir]) ], [
        incdirs="\
        /usr/include \
        /usr/local/include"

        for dir in $incdirs; do
            if test -r "$dir/cd.h"; then
               with_cd_incdir="$dir"
               break
            fi
        done
        if test -z "$with_cd_incdir"; then
            AC_MSG_RESULT(not found)
            AC_MSG_WARN([libcd header files not found, setting enable_cgm=no])
            with_cd_incdir=default
            enable_cgm=no
	else
	    AC_MSG_RESULT([found in $with_cd_incdir])
        fi ])
    else
    	AC_MSG_NOTICE("User-specified path to the libcd header files is $with_cd_incdir")
    fi
    if test "$with_cd_incdir" = "/usr/include"; then
        with_cd_incdir=default
    fi
    CDINCCMD=""
    CDINCPATH=""
    PL_ADD_TO_INCS($with_cd_incdir, CDINCCMD, CDINCPATH)
fi

PL_ARG_WITH(cd-libdir, [User-specified path to libcd], [])
if test "$enable_cgm" = "yes"; then
    if test -z "$with_cd_libdir"; then
        AC_CHECK_LIB(cd, main, [ with_cd_libdir=default ], [
        libdirs="\
        /usr/lib \
        /usr/local/lib"

        for dir in $libdirs; do
            if test -r "$dir/libcd.so" -o -r "$dir/libcd.a"; then
                with_cd_libdir="$dir"
                break
            fi
        done
        if test -z "$with_cd_libdir"; then
            AC_MSG_WARN([cd library not found, setting enable_cgm=no])
            with_cd_libdir=default
            enable_cgm=no
        fi ])
    else
    	AC_MSG_NOTICE("User-specified path to libcd is $with_cd_libdir")
    fi
    if test "$with_cd_libdir" = "/usr/lib"; then
        with_cd_libdir=default
    fi
    CDLIBSTR=-lcd
    CDLIBCMD=""
    CDLIBPATH=""
    PL_ADD_TO_LIBS($with_cd_libdir, $CDLIBSTR, CDLIBCMD, CDLIBPATH)
fi

AC_SUBST(CDINCCMD)
AC_SUBST(CDLIBCMD)

